plugins {
	id 'application'
	id 'com.gradleup.shadow' version '8.3.5'
	id 'com.google.protobuf' version '0.9.4'
}

repositories {
	maven {
		url 'https://oss.sonatype.org/content/repositories/snapshots/'
	}

	flatDir { dirs 'libs' }
}

project.version = '1.13.7'

dependencies {
	implementation project(':G3Utils'), project(':LrentNode'), project(':NavMap')
	implementation group: 'com.formdev', name: 'jide-oss', version: '3.7.15'
	implementation group: 'org.swinglabs.swingx', name: 'swingx-all', version: '1.6.5-1'
	implementation group: 'com.jgoodies', name: 'jgoodies-looks', version: '2.7.0'
	implementation group: 'com.glazedlists', name: 'glazedlists', version: '1.11.0'
	// MUST NOT be upgraded, breaks Java 1.8 compatibility.
	implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.4'
	implementation group: 'org.slf4j', name: 'jul-to-slf4j', version: '2.0.13'
	implementation group: 'uk.org.lidalia', name: 'sysout-over-slf4j-context', version: '2.0.0-SNAPSHOT'
	implementation group: 'uk.org.lidalia', name: 'sysout-over-slf4j-system', version: '2.0.0-SNAPSHOT'
	// Transitive dependencies of sysout-over-slf4j
	implementation group: 'uk.org.lidalia', name: 'lidalia-slf4j-ext', version: '1.0.0'
	implementation group: 'uk.org.lidalia', name: 'lidalia-lang', version: '1.0.0'
	implementation group: 'org.jmonkeyengine', name: 'jme3-core', version: '3.5.2-stable'
	implementation group: 'org.jmonkeyengine', name: 'jme3-desktop', version: '3.5.2-stable'
	implementation group: 'org.jmonkeyengine', name: 'jme3-lwjgl3', version: '3.5.2-stable'
	implementation group: 'io.github.classgraph', name: 'classgraph', version: '4.8.171'
	implementation group: 'org.zeromq', name: 'jeromq', version: '0.6.0'
	implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '2.6.1'
	implementation group: 'net.jodah', name: 'expiringmap', version: '0.5.11'
	implementation group: 'com.github.tulskiy', name: 'jkeymaster', version: '1.3-CUSTOM'
	// Transitive dependencies of jkeymaster
	implementation group: 'net.java.dev.jna', name: 'jna', version: '5.14.0'
	implementation group: 'net.java.dev.jna', name: 'jna-platform', version: '5.14.0'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.17.0'
	implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: '2.17.2'
	implementation group: 'com.j2html', name: 'j2html', version: '1.6.0'
	implementation group: 'hu.kazocsaba', name: 'image-viewer', version: '1.2.5-g3dit'
	implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.10.0'
	implementation group: 'net.sf.cssbox', name: 'cssbox', version: '4.17'
	implementation group: 'net.sf.cssbox', name: 'swingbox', version: '1.2-SNAPSHOT'
	implementation group: 'commons-io', name: 'commons-io', version: '2.16.1' // Used by net.sf.cssbox.swingbox
	implementation group: 'ca.phon.ui', name: 'jbreadcrumb', version: '4'
	implementation group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.9.0'
	implementation group: 'com.formdev', name: 'flatlaf', version: '3.5'
	implementation group: 'com.formdev', name: 'flatlaf-intellij-themes', version: '3.5'
	implementation group: 'com.formdev', name: 'flatlaf-extras', version: '3.4'
	implementation group: 'com.formdev', name: 'flatlaf-swingx', version: '3.5'
	implementation group: 'com.formdev', name: 'flatlaf-jide-oss', version: '3.5'
	implementation group: 'com.l2fprod.common', name: 'l2fprod-common-shared', version: '10.0'
	implementation group: 'com.l2fprod.common', name: 'l2fprod-common-sheet', version: '10.0'
	testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

protobuf {
	// Configure the protoc executable
	protoc {
		// Download from repositories
		artifact = 'com.google.protobuf:protoc:2.6.1'
	}
}

jar {
	manifest {
		attributes(
				'Class-Path': 'config/',
				'Main-Class': 'de.george.g3dit.Editor',
				'g3dit-Version': project.version
		)
	}
	processResources {
		exclude('logback-test.xml')
	}
}

application {
	mainClass = 'de.george.g3dit.Editor'
	applicationDefaultJvmArgs = ['-XX:+IgnoreUnrecognizedVMOptions', '-XX:+UseG1GC',
								 '-XX:MinHeapFreeRatio=5', '-XX:MaxHeapFreeRatio=20',
								 '--add-opens=java.desktop/java.awt=ALL-UNNAMED',
								 '--add-opens=java.desktop/javax.swing=ALL-UNNAMED',
								 '--add-opens=java.desktop/com.sun.java.swing.plaf.windows=ALL-UNNAMED',
								 '--add-opens=java.base/sun.nio.fs=ALL-UNNAMED',
								 '--add-opens=java.prefs/java.util.prefs=ALL-UNNAMED',
								 '-Djava.library.path=".;libs"']
}

tasks.run {
	workingDir = "$projectDir/root"
}

tasks.compileJava {
	dependsOn(project.parent.tasks.generateGettextSources)
}

tasks.shadowJar {
	mergeServiceFiles()
}

task copyBinlib(type: Copy) {
	dependsOn 'shadowJar'
	from("$projectDir/libs") {
		include "xfiledialog*.dll"
	}
	into "$projectDir/root/libs"
}

task copyJar(type: Copy) {
	dependsOn 'copyBinlib'
	from layout.buildDirectory.file("libs/${project.name}-${project.version}-all.jar")
	into "$projectDir/root/"
	rename { "${project.name}.jar" }
}

task dist(type: Zip) {
	dependsOn 'copyJar'
	into ('') {
		from 'root/' + project.name + '.exe'
		from 'root/' + project.name + 'c.exe'
		from 'root/' + project.name + '.bat'
		from 'root/' + project.name + 'c.bat'
		from 'root/' + project.name + '.sh'
		from 'root/' + project.name + 'c.sh'
		from 'root/' + project.name + '.jar'
		filePermissions {unix("rwxr-xr-x") }
	}
	into('libs') {
		from 'root/libs'
	}
	into('config') {
		from 'root/config'
		exclude 'g3dit*.json*'
	}
	destinationDirectory = file('dist')
	archiveFileName = project.name + '_' + project.version.replace(' ', '_') + ".zip"
}

task prefixNewMigrations {
	fileTree(dir: 'src/main', includes: ['java/de/george/g3dit/settings/migrations/*.java']).exclude({ isFilePrefixed(it.file) }).each { file ->
		doLast {
			def timestamp = new Date().format('yyyyMMddHHmmssSSS', TimeZone.getTimeZone('CET'))

			println "Renaming $file.name to ${timestamp}__$file.name"

			file.renameTo("${file.parentFile.absolutePath}${file.separator}V${timestamp}__${file.name}")

			// Sleep for a moment to avoid prefix conflicts when renaming multiple files
			sleep(1 * 1000)
		}
	}
}

def isFilePrefixed(file) {
	return (file.name ==~ '^V\\d+__.*\\.java\$')
}
